
#Область ОписаниеПеременных

Перем ВАЖНОСТЬ_ПРОПУСТИТЬ;
Перем ДОСТУПНЫЕ_ВАЖНОСТИ;
Перем ДОСТУПНЫЕ_КачествоПО;
Перем ДОСТУПНЫЕ_Атрибуты;

Перем _Лог;

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

Процедура Инициализировать(Знач КаталогиИсходников, Знач пУдалятьПоддержку, Знач пФильтрПоПодсистемам, Знач пЛог) Экспорт
	
	ВАЖНОСТЬ_ПРОПУСТИТЬ = "SKIP";
	ДОСТУПНЫЕ_ВАЖНОСТИ = Новый Массив;
	ДОСТУПНЫЕ_ВАЖНОСТИ.Добавить("HIGH");
	ДОСТУПНЫЕ_ВАЖНОСТИ.Добавить("MEDIUM");
	ДОСТУПНЫЕ_ВАЖНОСТИ.Добавить("LOW");
	ДОСТУПНЫЕ_ВАЖНОСТИ.Добавить(ВАЖНОСТЬ_ПРОПУСТИТЬ);
	
	ДОСТУПНЫЕ_КачествоПО = Новый Массив;
	ДОСТУПНЫЕ_КачествоПО.Добавить("MAINTAINABILITY");
	ДОСТУПНЫЕ_КачествоПО.Добавить("RELIABILITY");
	ДОСТУПНЫЕ_КачествоПО.Добавить("SECURITY");

	ДОСТУПНЫЕ_Атрибуты = Новый Массив;
	ДОСТУПНЫЕ_Атрибуты.Добавить("FORMATTED");
	ДОСТУПНЫЕ_Атрибуты.Добавить("CONVENTIONAL");
	ДОСТУПНЫЕ_Атрибуты.Добавить("IDENTIFIABLE");
	ДОСТУПНЫЕ_Атрибуты.Добавить("CLEAR");
	ДОСТУПНЫЕ_Атрибуты.Добавить("LOGICAL");
	ДОСТУПНЫЕ_Атрибуты.Добавить("COMPLETE");
	ДОСТУПНЫЕ_Атрибуты.Добавить("EFFICIENT");
	ДОСТУПНЫЕ_Атрибуты.Добавить("FOCUSED");
	ДОСТУПНЫЕ_Атрибуты.Добавить("DISTINCT");
	ДОСТУПНЫЕ_Атрибуты.Добавить("MODULAR");
	ДОСТУПНЫЕ_Атрибуты.Добавить("TESTED");
	ДОСТУПНЫЕ_Атрибуты.Добавить("LAWFUL");
	ДОСТУПНЫЕ_Атрибуты.Добавить("TRUSTWORTHY");
	ДОСТУПНЫЕ_Атрибуты.Добавить("RESPECTFUL");
	
	ПрименениеНастроекПоФайлу.Инициализировать(КаталогиИсходников, пУдалятьПоддержку, пФильтрПоПодсистемам, пЛог);
	
	_Лог = пЛог;

КонецПроцедуры

Функция ПрименитьНастройкиКПравилам(замечанияФайла, таблицаНастроек) Экспорт
	
	Результат = ПрименениеНастроекПоФайлу.ПустойРезультатПримененияПравил();

	всегоПравил = замечанияФайла.rules.Количество();
	
	Для ц = 1 По всегоПравил Цикл
		
		цПравило = замечанияФайла.rules[всегоПравил - ц];
		
		Для каждого цСтрока Из таблицаНастроек Цикл
			
			Если цПравило.impacts[0].severity = ВАЖНОСТЬ_ПРОПУСТИТЬ Тогда
				// Пропуск работает по принципу - применяем первое попавшееся,
				// когда как остальные настройки - последнее попавшееся.
				Прервать;
			КонецЕсли;
			
			Если ПрименениеНастроекПоФайлу.НастройкаПрименима(цПравило.Id, цСтрока.ruleId) Тогда
			
				Если ПрименитьНастройкуКПравилу(цПравило, цСтрока) Тогда
					
					Результат.ЕстьИзменения = Истина;
					
				КонецЕсли;

			КонецЕсли;
			
		КонецЦикла;
		
		Если цПравило.impacts[0].severity = ВАЖНОСТЬ_ПРОПУСТИТЬ Тогда
				
			замечанияФайла.rules.Удалить(всегоПравил - ц);
			Результат.ЕстьИзменения = Истина;
			Результат.ПропущенныеПравила.Добавить(цПравило.Id);
			Продолжить;
			
		КонецЕсли;
		
	КонецЦикла;

	Возврат Результат;

КонецФункции

Функция ПрименитьНастройкиКЗамечаниям(замечанияФайла, таблицаНастроек, пропущенныеПравила = Неопределено) Экспорт
	
	файлИзменен = Ложь;

	всегоЗамечаний = замечанияФайла.issues.Количество();
	
	Для ц = 1 По всегоЗамечаний Цикл
		
		цЗамечание = замечанияФайла.issues[всегоЗамечаний - ц];
		
		Если ОшибкаВЗамечании(цЗамечание)
			ИЛИ ФайлНаПоддержке(цЗамечание)
			ИЛИ ИсключитьЗамечаниеФильтромПоПодсистеме(цЗамечание)
			ИЛИ ПрименениеНастроекПоФайлу.ПравилоПропущено(цЗамечание, пропущенныеПравила) Тогда
			
			замечанияФайла.issues.Удалить(всегоЗамечаний - ц);
			файлИзменен = Истина;
			Продолжить;
			
		КонецЕсли;
		
		Если ПрименитьНастройкиКЗамечанию(цЗамечание, таблицаНастроек) Тогда
			
			файлИзменен = Истина;
			
		КонецЕсли;
		
	КонецЦикла;

	Возврат файлИзменен;

КонецФункции

Функция ЭлементНастройки_Generic_Issue() Экспорт
	
	структ = Новый Структура;
	
	структ.Вставить("ruleId", "");
	структ.Вставить("message", "");
	структ.Вставить("filePath", "");
	структ.Вставить("effortMinutes", 0);
	структ.Вставить("severity", "");
	структ.Вставить("cleanCodeAttribute", "");
	структ.Вставить("softwareQuality", "");
	
	Возврат структ;
	
КонецФункции

Функция ПереопределяемыеПараметрыОшибки() Экспорт
	
	Структура = Новый Структура;
	Структура.Вставить("severity", "");
	Структура.Вставить("cleanCodeAttribute", "");
	Структура.Вставить("softwareQuality", "");
	
	Возврат Структура;
	
КонецФункции

Процедура ДобавитьВНастройкиЗначенияИзФайла(Настройки, Знач Файл, Знач Лог) Экспорт
	
	ошибкиФайла = ОбщегоНазначения.ПрочитатьJSONФайл(Файл, Лог);
	
	Если НЕ ТипЗнч(ошибкиФайла) = Тип("Структура")
		ИЛИ НЕ ошибкиФайла.Свойство("issues")
		ИЛИ НЕ ТипЗнч(ошибкиФайла.issues) = Тип("Массив") Тогда
		
		Лог.Ошибка("Не поддерживаемая структура файла: " + Файл);
		Возврат;
		
	КонецЕсли;

	Если НЕ ошибкиФайла.Свойство("rules")
		ИЛИ НЕ ТипЗнч(ошибкиФайла.rules) = Тип("Массив") Тогда
		
		Лог.Ошибка("Не поддерживаемая структура файла: " + Файл);
		Возврат;
		
	КонецЕсли;
	
	Для Каждого цОшибка Из ошибкиФайла.issues Цикл
		
		прочитанныеНастройки = ПереопределяемыеПараметрыОшибки();
		ЗаполнитьЗначенияСвойств(прочитанныеНастройки, цОшибка);
		
		Настройки.Ишузы.Вставить(цОшибка.ruleId, прочитанныеНастройки);
		
	КонецЦикла;
	
	Для Каждого цПравило Из ошибкиФайла.rules Цикл
		
		Если цПравило.impacts.Количество() = 0 Тогда
			
			Лог.Ошибка("Неподдерживаемая структура файла: " + Файл);
			Продолжить;
			
		КонецЕсли;
		
		параметрыПравила = ПереопределяемыеПараметрыОшибки();
		параметрыПравила.cleanCodeAttribute = цПравило.cleanCodeAttribute;
		параметрыПравила.softwareQuality = цПравило.impacts[0].softwareQuality;
		параметрыПравила.severity = цПравило.impacts[0].severity;
		
		Настройки.Правила.Вставить(цПравило.Id, параметрыПравила);
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ПрименитьНастройкуКПравилу(пПравило, Знач пСтрокаНастроек)

	естьИзменения = Ложь;
	заголовокЛога = СтрШаблон("Id: <%1>. Установлено ", пПравило.Id);

	Если Не пСтрокаНастроек.severity = пПравило.impacts[0].severity
		И Не ДОСТУПНЫЕ_ВАЖНОСТИ.Найти(пСтрокаНастроек.severity) = Неопределено Тогда
		
		_лог.Отладка(заголовокЛога + "severity: " + пСтрокаНастроек.severity);
		
		пПравило.impacts[0].severity = пСтрокаНастроек.severity;
		естьИзменения = Истина;
		
	КонецЕсли;
	
	Если Не пСтрокаНастроек.softwareQuality = пПравило.impacts[0].softwareQuality
		И Не ДОСТУПНЫЕ_КачествоПО.Найти(пСтрокаНастроек.softwareQuality) = Неопределено Тогда
		
		_лог.Отладка(заголовокЛога + "softwareQuality: " + пСтрокаНастроек.softwareQuality);
		
		пПравило.impacts[0].softwareQuality = пСтрокаНастроек.softwareQuality;
		естьИзменения = Истина;
		
	КонецЕсли;

	Если Не пСтрокаНастроек.cleanCodeAttribute = пПравило.cleanCodeAttribute
		И Не ДОСТУПНЫЕ_Атрибуты.Найти(пСтрокаНастроек.cleanCodeAttribute) = Неопределено Тогда
		
		_лог.Отладка(заголовокЛога + "cleanCodeAttribute: " + пСтрокаНастроек.cleanCodeAttribute);
		
		пПравило.cleanCodeAttribute = пСтрокаНастроек.cleanCodeAttribute;
		естьИзменения = Истина;
		
	КонецЕсли;

	Возврат естьИзменения;
	
КонецФункции

Функция ОшибкаВЗамечании(Знач пОшибка)
	
	путьКФайлу = пОшибка.primaryLocation.filePath;
	
	Если Не ЗначениеЗаполнено(путьКФайлу) Тогда
		
		_Лог.Ошибка("Не указан путь для ошибки: %1. %2", пОшибка.ruleId, пОшибка.primaryLocation.message);
		
		Возврат Истина;
		
	КонецЕсли;

	Возврат Ложь;

КонецФункции

Функция ФайлНаПоддержке(Знач пОшибка)
	
	Возврат ПрименениеНастроекПоФайлу.ФайлНаПоддержке(пОшибка.primaryLocation.filePath);
	
КонецФункции

Функция ИсключитьЗамечаниеФильтромПоПодсистеме(Знач пОшибка)
	
	Возврат ПрименениеНастроекПоФайлу.ИсключитьЗамечаниеФильтромПоПодсистеме(пОшибка.primaryLocation.filePath);

КонецФункции

Функция ПрименитьНастройкиКЗамечанию(пОшибка, таблицаНастроек)
	
	естьИзменения = Ложь;
	
	ruleId = пОшибка.ruleId;
	message = пОшибка.primaryLocation.message;
	
	filePath = ПутьКФайлуСЗамечаниями(пОшибка);
	
	Для каждого цСтрока Из таблицаНастроек Цикл
		
		Если ПрименениеНастроекПоФайлу.НастройкаПрименима(ruleId, цСтрока.ruleId)
			И ПрименениеНастроекПоФайлу.НастройкаПрименима(filePath, цСтрока.filePath)
			И ПрименениеНастроекПоФайлу.НастройкаПрименима(message, цСтрока.message) Тогда

			Если ПрименитьНастройкуКЗамечанию(цСтрока, пОшибка) Тогда
				естьИзменения = Истина;
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат естьИзменения;
	
КонецФункции

Функция ПрименитьНастройкуКЗамечанию(Знач пСтрокаНастроек, пОшибка)

	естьИзменения = Ложь;
	заголовокЛога = СтрШаблон("ruleId: <%1>, message: <%2>, filePath: <%3>. Установлено ",
			пОшибка.ruleId,
			пОшибка.primaryLocation.message,
			пОшибка.primaryLocation.filePath);
			
	Если пОшибка.Свойство("effortMinutes")
		И ЗначениеЗаполнено(пОшибка.effortMinutes) Тогда
		
		затратыНаОшибку = пОшибка.effortMinutes;
		
	Иначе
		
		затратыНаОшибку = 0;

	КонецЕсли;

	Если ТипЗнч(пСтрокаНастроек.effortMinutes) = Тип("Число")
		И Не пСтрокаНастроек.effortMinutes = затратыНаОшибку Тогда
		
		_лог.Отладка(заголовокЛога + "effortMinutes: " + пСтрокаНастроек.effortMinutes);
		
		пОшибка.Вставить("effortMinutes", пСтрокаНастроек.effortMinutes);
		естьИзменения = Истина;
		
	КонецЕсли;
	
	Возврат естьИзменения;
	
КонецФункции

Функция ПутьКФайлуСЗамечаниями(пОшибка)
	
	путьКФайлуСЗамечанием = пОшибка.primaryLocation.filePath;

	filePath = ПрименениеНастроекПоФайлу.ОбеспечитьПутьКФайлуСИсходнымКодом(путьКФайлуСЗамечанием);
	
	Возврат filePath;
	
КонецФункции

#КонецОбласти