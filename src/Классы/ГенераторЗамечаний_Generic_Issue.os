#Область ОписаниеПеременных

Перем _Лог;
Перем _ИспользоватьОтносительныеПути;
Перем _ТекущийРабочийКаталог;

#КонецОбласти

Процедура ПриСозданииОбъекта(Знач пЛог, Знач пИспользоватьОтносительныеПути = Ложь)
	
	_Лог = пЛог;
	_ИспользоватьОтносительныеПути = пИспользоватьОтносительныеПути;
	_ТекущийРабочийКаталог = "";
	
	Если _ИспользоватьОтносительныеПути Тогда
		
		файл = Новый Файл(".");
		_ТекущийРабочийКаталог = СтрЗаменить(файл.ПолноеИмя, "\", "/");
		
	КонецЕсли;
	
КонецПроцедуры

#Область ПрограммныйИнтерфейс

Функция ИсточникПроверки() Экспорт
	
	Возврат "edt";
	
КонецФункции

Функция МестонахождениеОшибки(Знач пДанные) Экспорт
	
	структ = Новый Структура;
	
	структ.Вставить("message", СообщениеОбОшибке(пДанные));
	структ.Вставить("filePath", ПутьКФайлу(пДанные));
	структ.Вставить("textRange", КоординатыОшибки(пДанные));
	
	Возврат структ;
	
КонецФункции

Функция СообщениеОбОшибке(Знач пДанные) Экспорт
	
	Возврат пДанные.Описание;
	
КонецФункции

Функция ПутьКФайлу(Знач пДанные) Экспорт
	
	путь = пДанные.Путь;
	
	путь = СтрЗаменить(путь, "\", "/"); // винда может жить с прямым слешем, а вот линукс с обратным не очень
	
	Если _ИспользоватьОтносительныеПути Тогда
		
		путь = СтрЗаменить(путь, _ТекущийРабочийКаталог, ".");
		
	КонецЕсли;
	
	Возврат путь;
	
КонецФункции

Функция КоординатыОшибки(Знач пДанные) Экспорт
	
	структ = Новый Структура;
	МинВалиднаяПозиция = 1;
	
	Попытка
		Позиция = Число(пДанные.НомерСтроки);
		структ.Вставить("startLine", ?(Позиция = 0, МинВалиднаяПозиция, Позиция)); // для сонара 0 невалидная строка
	Исключение
		_Лог.Ошибка("Не удалось преобразовать к числу номер строки: " + пДанные.НомерСтроки);
		структ.Вставить("startLine", МинВалиднаяПозиция);
	КонецПопытки;
	
	Возврат структ;
	
КонецФункции

#КонецОбласти
